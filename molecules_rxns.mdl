/******************************************************/
/* A simple model of a single AZ at the calyx of Held */
/*                                                    */
/* Definition of AMPARs                               */
/*                                                    */
/* Matthias Hennig                                    */
/* mhennig@inf.ed.ac.uk                               */
/* Updated by Jacob Czech                             */
/* czech.jacob@gmail.com                              */
/******************************************************/

/* the transmitter in question */
DEFINE_MOLECULES
{
  Glu { DIFFUSION_CONSTANT_3D = D }

  AMPAR_C0 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_C1 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_C2 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_C3 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_C4 { DIFFUSION_CONSTANT_2D = 0 }

  AMPAR_O1 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_O2 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_O3 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_O4 { DIFFUSION_CONSTANT_2D = 0 }

  AMPAR_D1 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_D2 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_D3 { DIFFUSION_CONSTANT_2D = 0 }
  AMPAR_D4 { DIFFUSION_CONSTANT_2D = 0 }
}

DEFINE_REACTIONS
{
  /*C0[>C1{  Rb:+Glu,POSITIVE_POLE}]*/
  AMPAR_C0' + Glu' -> AMPAR_C1' [Rb]

  /*C1[>C2{2*Rb:+Glu,POSITIVE_POLE}][>O1{Ro1}]  [>C0{4*Ru1:-Glu,POSITIVE_POLE}][>D1{Rd0}]*/
  AMPAR_C1' + Glu' -> AMPAR_C2' [2*Rb]
  AMPAR_C1' -> AMPAR_O1' [Ro1]
  AMPAR_C1' -> AMPAR_C0' + Glu' [4*Ru1]
  AMPAR_C1' -> AMPAR_D1' [Rd0]

  /*C2[>C3{3*Rb:+Glu,POSITIVE_POLE}][>O2{Ro1*2}][>C1{3*Ru1:-Glu,POSITIVE_POLE}][>D2{Rd1*2}]*/
  AMPAR_C2' + Glu' -> AMPAR_C3' [3*Rb]
  AMPAR_C2' -> AMPAR_O2' [Ro1*2]
  AMPAR_C2' -> AMPAR_C1' + Glu' [3*Ru1]
  AMPAR_C2' -> AMPAR_D2' [Rd1*2]

  /*C3[>C4{4*Rb:+Glu,POSITIVE_POLE}][>O3{Ro1*3}][>C2{2*Ru1:-Glu,POSITIVE_POLE}][>D3{Rd1*3}]*/
  AMPAR_C3' + Glu' -> AMPAR_C4' [4*Rb]
  AMPAR_C3' -> AMPAR_O3' [Ro1*3]
  AMPAR_C3' -> AMPAR_C2' + Glu' [2*Ru1]
  AMPAR_C3' -> AMPAR_D3' [Rd1*3]

  /*C4[>O4{Ro1*4}][>C3{Ru1:-Glu,POSITIVE_POLE}][>D4{Rd1*4}]*/
  AMPAR_C4' -> AMPAR_O4' [Ro1*4]
  AMPAR_C4' -> AMPAR_C3' + Glu' [Ru1]
  AMPAR_C4' -> AMPAR_D4' [Rd1*4]

  /*O1[>C1{Rc1}]*/
  AMPAR_O1' -> AMPAR_C1' [Rc1]
  /*O2[>C2{Rc1}]*/
  AMPAR_O2' -> AMPAR_C2' [Rc1]
  /*O3[>C3{Rc1}]*/
  AMPAR_O3' -> AMPAR_C3' [Rc1]
  /*O4[>C4{Rc1}]*/
  AMPAR_O4' -> AMPAR_C4' [Rc1]

  /*D1[>C1{Rr1}][>D2{Rd2:+Glu,POSITIVE_POLE}]*/
  AMPAR_D1' -> AMPAR_C1' [Rr1]
  AMPAR_D1' + Glu' -> AMPAR_D2' [Rd2]
  /*D2[>C2{Rr1}][>D3{2*Rd2:+Glu,POSITIVE_POLE}][>D1{3*Rr2:-Glu,POSITIVE_POLE}]*/
  AMPAR_D2' -> AMPAR_C2' [Rr1]
  AMPAR_D2' + Glu' -> AMPAR_D3' [2*Rd2]
  AMPAR_D2' -> AMPAR_D1' + Glu' [3*Rr2]
  /*D3[>C3{Rr1}][>D4{3*Rd2:+Glu,POSITIVE_POLE}][>D2{2*Rr2:-Glu,POSITIVE_POLE}]*/
  AMPAR_D3' -> AMPAR_C3' [Rr1]
  AMPAR_D3' + Glu' -> AMPAR_D4' [3*Rd2]
  AMPAR_D3' -> AMPAR_D2' + Glu' [2*Rr2]
  /*D4[>C4{Rr1}][>D3{Rr2:-Glu,POSITIVE_POLE}]*/
  AMPAR_D4' -> AMPAR_C4' [Rr1]
  AMPAR_D4' -> AMPAR_D3' + Glu' [Rr2]
}

/* AMPA receptor kinetics */
/*DEFINE_REACTION AMPAR {*/
/*  C0[>C1{  Rb:+Glu,POSITIVE_POLE}]*/
/*  C1[>C2{2*Rb:+Glu,POSITIVE_POLE}][>O1{Ro1}]  [>C0{4*Ru1:-Glu,POSITIVE_POLE}][>D1{Rd0}]*/
/*  C2[>C3{3*Rb:+Glu,POSITIVE_POLE}][>O2{Ro1*2}][>C1{3*Ru1:-Glu,POSITIVE_POLE}][>D2{Rd1*2}]*/
/*  C3[>C4{4*Rb:+Glu,POSITIVE_POLE}][>O3{Ro1*3}][>C2{2*Ru1:-Glu,POSITIVE_POLE}][>D3{Rd1*3}]*/
/*  C4[>O4{Ro1*4}][>C3{Ru1:-Glu,POSITIVE_POLE}][>D4{Rd1*4}]*/

/*  O1[>C1{Rc1}]*/
/*  O2[>C2{Rc1}]*/
/*  O3[>C3{Rc1}]*/
/*  O4[>C4{Rc1}]*/

/*  D1[>C1{Rr1}][>D2{Rd2:+Glu,POSITIVE_POLE}]*/
/*  D2[>C2{Rr1}][>D3{2*Rd2:+Glu,POSITIVE_POLE}][>D1{3*Rr2:-Glu,POSITIVE_POLE}]*/
/*  D3[>C3{Rr1}][>D4{3*Rd2:+Glu,POSITIVE_POLE}][>D2{2*Rr2:-Glu,POSITIVE_POLE}]*/
/*  D4[>C4{Rr1}][>D3{Rr2:-Glu,POSITIVE_POLE}]*/

/*  REFERENCE_STATE C0 {*/
/*    Glu*/
/*    NUMBER_BOUND = 0*/
/*  }*/
/*}*/
